# Query 1 of the TPC Benchmark.
#
# July 2004, M. Spiekermann
#
#select
#       l_returnflag,
#       l_linestatus,
#       sum(l_quantity) as sum_qty,
#       sum(l_extendedprice) as sum_base_price,
#       sum(l_extendedprice * (1 - l_discount)) as sum_disc_price,
#       sum(l_extendedprice * (1 - l_discount) * (1 + l_tax)) as sum_charge,
#       avg(l_quantity) as avg_qty,
#       avg(l_extendedprice) as avg_price,
#       avg(l_discount) as avg_disc,
#       count(*) as count_order
#from
#       lineitem
#where
#       l_shipdate <= date '1998-12-01' - interval '90' day (3)
#group by
#       l_returnflag,
#       l_linestatus
#order by
#       l_returnflag,
#       l_linestatus;
#
# Remark.: For query validation interval must be 90. For performance measurment it could be
#          randomly selected between [60, 120].

query LINEITEM feed 
      project [ L_RETURNFLAG, L_LINESTATUS, L_SHIPDATE, L_QUANTITY, L_EXTENDENPRICE, L_DISCOUNT, L_TAX ]
      filter  [ .L_SHIPDATE < [const instant value "1998-12-01"] - [const duration value (90 0)] ] 
      sortby  [ L_RETURNFLAG asc, L_LINESTATUS asc]
      groupby [ L_RETURNFLAG, L_LINESTATUS; SUM_QTY : group feed sum[L_QUANTITY],
                  SUM_BASE_PRICE : group feed sum[L_EXTENDENPRICE],
                  SUM_DISC_PRICE : group feed extend[L_EXPR_MULT : .L_EXTENDENPRICE * (1 - .L_DISCOUNT)] sum[L_EXPR_MULT],
                  SUM_CHARGE : group feed extend[L_EXPR_MULT2 : .L_EXTENDENPRICE * (1 - .L_DISCOUNT) * (1 + .L_TAX)] sum[L_EXPR_MULT2],
                  AVG_QTY : group feed avg[L_QUANTITY],
                  AVG_PRICE : group feed avg[L_EXTENDENPRICE],
                  AVG_DISC : group feed avg[L_DISCOUNT],
                  COUNT_ORDER : group feed count ]
      consume;
